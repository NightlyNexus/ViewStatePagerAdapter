buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:3.3.1'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.16'
  }

  repositories {
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

allprojects {
  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all',
                             '-Xlint:-deprecation',
                             '-Werror']
  }

  apply plugin: 'net.ltgt.errorprone'
  apply plugin: 'checkstyle'

  repositories {
    google()
    jcenter() // Needed for Lint.
  }

  dependencies {
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'
  }

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
  }

  checkstyle {
    toolVersion = '7.7'
  }
}
